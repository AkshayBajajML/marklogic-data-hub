buildscript {
    repositories {
        mavenCentral()
        maven {url "http://developer.marklogic.com/maven2/"}
        maven {url "http://rjrudin.github.io/marklogic-java/releases"}
    }
    dependencies {
        classpath mlGradleDependency
        classpath propertiesPluginDependency
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
    maven {url "http://developer.marklogic.com/maven2/"}
    maven {url "http://rjrudin.github.io/marklogic-java/releases"}
}

dependencies {
    compile 'com.marklogic:marklogic-xcc:8.0.4'
    compile 'com.marklogic:java-client-api:3.0.3'
    compile 'log4j:log4j:1.2.17'
    compile 'commons-io:commons-io:2.4'
    testCompile 'junit:junit:4.12'
    testCompile 'xmlunit:xmlunit:1.3'
}

// this plugin allows us to override the gradle.properties file per environment
apply plugin: 'net.saliman.properties'

apply plugin: 'ml-gradle'

ext {
    mlAppConfig {
        modulePaths.add("src/hub-in-a-box")
        contentXccUrl = "xcc://${mlUsername}:${mlPassword}@${mlHost}:${mlRestPort}/${mlAppConfig.contentDatabaseName}"
    }
}

task generateRewriter(type: com.marklogic.gradle.task.XccTask) {
    String custom = new File("src/hub-in-a-box/rewriter.xml").getText('UTF-8')
    String xqueryToRun = new File("src/hub-in-a-box/build-rewriter.xqy").getText('UTF-8')
    xqueryToRun = xqueryToRun.replace('__CUSTOM_REWRITER__', custom)
    xccUrl = contentXccUrl
    xquery = xqueryToRun
}

mlPostDeploy.dependsOn generateRewriter
