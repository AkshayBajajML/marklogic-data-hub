buildscript {
    repositories {
        jcenter()
        maven { url 'http://developer.marklogic.com/maven2' }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
    }
}

plugins {
    id 'net.saliman.properties' version '1.4.5'
    id "groovy"
    id "java-gradle-plugin"
    id "maven-publish"
    id "eclipse"
    id "idea"
    id "com.jfrog.bintray" version "1.7.1"
}

apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven { url 'http://developer.marklogic.com/maven2' }
}

dependencies {
    compile gradleApi()
    compile project(":marklogic-data-hub")
    compile 'com.marklogic:marklogic-xcc:8.0.5'
    compile 'com.marklogic:ml-gradle:2.3.4'

    testCompile localGroovy()
    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4-rc-2') {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = "com.marklogic.ml-data-hub"
            implementationClass = "com.marklogic.gradle.DataHubPlugin"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allJava
    from sourceSets.main.allGroovy
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['main']
    pkg {
        repo = 'maven'
        name = 'ml-data-hub'
        desc = 'A gradle plugin for Data Hub tasks'
        // userOrg = user
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/marklogic/marklogic-data-hub'
        issueTrackerUrl = 'https://github.com/marklogic/marklogic-data-hub/issues'
    }
}

pluginBundle {
    website = 'http://www.gradle.org/'
    vcsUrl = 'git@github.com:marklogic/marklogic-data-hub.git'
    description = 'ml-data-hub A gradle plugin for Data Hub tasks'
    tags = ['marklogic', 'data hub']

    plugins {
        mldatahubPlugin {
            id = 'com.marklogic.ml-data-hub'
            displayName = 'Gradle tasks for MarkLogic Data Hub'
            description = 'Gradle plugin for configuring and deploying the MarkLogic Data Hub'
            tags = ['marklogic', 'data hub']
        }
    }
}
